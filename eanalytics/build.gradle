apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.github.dcendents.android-maven"

def libName = 'eanalytics'
def libDescription = 'Android tracking library for Eulerian Technologies Analytics.'
def publishedGroupId = 'com.eulerian.android.sdk'
def artifact = 'eanalytics'
def siteUrl = 'https://github.com/EulerianTechnologies/eanalytics-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/EulerianTechnologies/eanalytics-android.git'   // Git repository URL
def developerId = 'frouo'
def developerName = 'Francois Rouault'
def developerEmail = 'francois@eulerian.com'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    resourcePrefix "eanalytics__"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName rootProject.ext.eanalyticsVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api "com.google.android.gms:play-services-ads:16.0.0"
    api 'com.android.installreferrer:installreferrer:1.1.2'
}

// Specify POM Details :
version = rootProject.ext.eanalyticsVersionName // This is the library version used when deploying the artifact
group = publishedGroupId // Maven Group ID for the artifact

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavaDocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavaDocsJar(type: Jar) {
    from generateJavaDocs.destinationDir
    classifier 'javadoc'
}
generateJavaDocsJar.dependsOn generateJavaDocs

//---
// Configure the Bintray Plugin
// READ ME: write your bintray user and apiKey in local.properties file.
//---

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apiKey")

    publish = true
    override = false //Whether to override version artifacts already published

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libName
        userOrg = 'euleriantechnologies'
        desc = libDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'EAnalytics'
                description 'Android tracking library for Eulerian Technologies Analytics.'
                url 'eulerian.com'
                developers {
                    developer {
                        name 'Fran√ßois Rouault'
                        email 'francois@eulerian.com'
                    }
                }
            }
        }
    }
}

artifacts {
    archives generateJavaDocsJar
    archives generateSourcesJar
}
